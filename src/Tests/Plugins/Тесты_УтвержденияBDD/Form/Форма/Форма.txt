&НаКлиенте
Перем КонтекстЯдра;
&НаКлиенте
Перем Ожидаем;

&НаКлиенте
Процедура Инициализация(КонтекстЯдраПараметр) Экспорт
	КонтекстЯдра = КонтекстЯдраПараметр;
	Ожидаем = КонтекстЯдра.Плагин("УтвержденияBDD");
КонецПроцедуры

&НаКлиенте
Функция ПолучитьСписокТестов() Экспорт
	ВсеТесты = Новый Массив;
	ВсеТесты.Добавить("ТестДолжен_Проверить_РаботуУтвержденийBDD_НаКлиенте");
	ВсеТесты.Добавить("ТестДолжен_Проверить_РаботуКомпозитныхУтвержденийBDD_НаКлиенте");
	ВсеТесты.Добавить("ТестДолжен_Проверить_ВыбрасываемыеСообщения_НаКлиенте");
	
	Возврат ВсеТесты;
КонецФункции

&НаКлиенте
Процедура ПередЗапускомТеста() Экспорт
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗапускаТеста() Экспорт
	
КонецПроцедуры

&НаКлиенте
Процедура ТестДолжен_Проверить_РаботуУтвержденийBDD_НаКлиенте() Экспорт
	Ожидаем.Что(1 > 0, "ЭтоИстина").ЭтоИстина();
	Ожидаем.Что(13 = 2, "Не ЭтоИстина").Не_().ЭтоИстина();
	Ожидаем.Что(Неопределено, "Неопределено <> Истина").Не_().ЭтоИстина();
	Ожидаем.Что(5 = 7, "ЭтоЛожь").ЭтоЛожь();
	Ожидаем.Что(Неопределено, "Неопределено <> Ложь").Не_().ЭтоЛожь();
	Ожидаем.Что(5, "Равно").Равно(5);
	Ожидаем.Что(4, "Больше").Больше(2);
	Ожидаем.Что(7, "БольшеИлиРавно").БольшеИлиРавно(7);
	Ожидаем.Что(15, "Минимум").Минимум(9);
	Ожидаем.Что(90, "МеньшеИлиРавно").МеньшеИлиРавно(100);
	Ожидаем.Что(90, "Максимум").Максимум(90);
	Ожидаем.Что(55, "Меньше").Меньше(56);
	Ожидаем.Что(1, "Заполнено").Заполнено();
	Ожидаем.Что(Новый Массив, "Существует").Существует();
	Ожидаем.Что(Неопределено, "ЭтоНеопределено").ЭтоНеопределено();
	Ожидаем.Что(Null, "ЭтоNull").ЭтоNull();
	Ожидаем.Что("", "ИмеетТип").ИмеетТип("Строка");
	Ожидаем.Что(7, "Между").Между(1, 10);
	Ожидаем.Что("Некая строка", "Содержит").Содержит("стр");
	Ожидаем.Что("Некая строка", "ИмеетДлину").ИмеетДлину(12);
	Ожидаем.Что(Новый Структура).Метод("Вставить").ВыбрасываетИсключение("Недостаточно фактических параметров");
	Ожидаем.Что(Истина, "ЕстьИстина").ЕстьИстина().Существует();
	Ожидаем.Что(Ложь, "ЕстьЛожь").ЕстьЛожь().Существует();
	Ожидаем.Что(Неопределено, "ЕстьНеопределено и ЭтоНе").ЕстьНеопределено().ЭтоНе().Существует();
	Ожидаем.Что(Null, "ЕстьNull и ЭтоНе").ЕстьNull().ЭтоНе().Существует();
КонецПроцедуры

&НаКлиенте
Процедура ТестДолжен_Проверить_РаботуКомпозитныхУтвержденийBDD_НаКлиенте() Экспорт
	Ожидаем.Что(Новый Массив, "Пустой массив")
		.Существует()
		.Не_().Заполнено()
		.ИмеетДлину(0)
		.Не_().Содержит(5);
КонецПроцедуры

&НаКлиенте
Процедура ТестДолжен_Проверить_ВыбрасываемыеСообщения_НаКлиенте() Экспорт
	ТекстИсключения = "";
	Попытка
		Ожидаем.Что(Ложь).ЭтоИстина();
	Исключение
		ТекстИсключения = ОписаниеОшибки();
	КонецПопытки;
	Ожидаем.Что(ТекстИсключения).Не_().Содержит("НЕ");
	
	ТекстИсключения = "";
	Попытка
		Ожидаем.Что(Истина).Не_().ЭтоИстина();
	Исключение
		ТекстИсключения = ОписаниеОшибки();
	КонецПопытки;
	Ожидаем.Что(ТекстИсключения).Содержит("НЕ");
КонецПроцедуры
