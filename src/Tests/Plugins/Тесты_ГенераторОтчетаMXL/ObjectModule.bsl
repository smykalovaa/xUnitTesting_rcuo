Перем КонтекстЯдра;
Перем Ожидаем;

Процедура Инициализация(КонтекстЯдраПараметр) Экспорт
	КонтекстЯдра = КонтекстЯдраПараметр;
	Ожидаем = КонтекстЯдра.Плагин("УтвержденияBDD");
КонецПроцедуры

Функция ПолучитьСписокТестов() Экспорт
	ВсеТесты = Новый Массив;
	ВсеТесты.Добавить("ТестДолжен_ПолучитьОтчетОТестировании");
	
	Возврат ВсеТесты;
КонецФункции

Процедура ПередЗапускомТеста() Экспорт
	
КонецПроцедуры

Процедура ПослеЗапускаТеста() Экспорт
	
КонецПроцедуры

Процедура ТестДолжен_ПолучитьОтчетОТестировании() Экспорт
	ПостроительДереваТестов = КонтекстЯдра.Плагин("ПостроительДереваТестов");
	УспешныйЭлемент = ПостроительДереваТестов.СоздатьЭлемент(ЭтотОбъект.ИспользуемоеИмяФайла, "УспешныйМетод");
	ПадающийЭлемент = ПостроительДереваТестов.СоздатьЭлемент(ЭтотОбъект.ИспользуемоеИмяФайла, "ПадающийМетод");
	ОтсутствующийЭлемент = ПостроительДереваТестов.СоздатьЭлемент(ЭтотОбъект.ИспользуемоеИмяФайла, "ОтсутствующийМетод", "Отсутствующий метод");
	
	Контейнер = ПостроительДереваТестов.СоздатьКонтейнер("Контейнер");
	Контейнер.Строки.Добавить(УспешныйЭлемент);
	Контейнер.Строки.Добавить(ПадающийЭлемент);
	Контейнер.Строки.Добавить(ОтсутствующийЭлемент);
	
	РезультатТестирования = КонтекстЯдра.ВыполнитьТесты(ЗагрузчикЗаглушка(), Контейнер);
	
	ГенераторОтчетаMXL = КонтекстЯдра.Плагин("ГенераторОтчетаMXL");
	Отчет = ГенераторОтчетаMXL.СоздатьОтчет(КонтекстЯдра, РезультатТестирования);
	
	Ожидаем.Что(Отчет).ИмеетТип("ТабличныйДокумент");
	
	// Заголовок
	ОбщаяСтатистикаЗапуска = "Запущено тестов - " + Контейнер.Строки.Количество() + ", ошибочных тестов - 1, не прошло проверку - 0, пропущено - 1";
	Ожидаем.Что(Отчет.НайтиТекст(ОбщаяСтатистикаЗапуска), "ОбщаяСтатистикаЗапуска").Существует();
	Ожидаем.Что(Отчет.НайтиТекст("Общее время выполнения"), "Заголовок").Существует();
	// Контейнер
	Ожидаем.Что(Отчет.НайтиТекст(Контейнер.Имя), "Контейнер").Существует();
	// Тесты
	Ожидаем.Что(Отчет.НайтиТекст(УспешныйЭлемент.ИмяМетода), "УспешныйЭлемент").Не_().Существует();
	Ожидаем.Что(Отчет.НайтиТекст(ПадающийЭлемент.ИмяМетода), "ПадающийЭлемент").Существует();
	Ожидаем.Что(Отчет.НайтиТекст(ОтсутствующийЭлемент.ИмяМетода+", путь"), "ОтсутствующийЭлемент").Не_().Существует();
	Ожидаем.Что(Отчет.НайтиТекст(ОтсутствующийЭлемент.Представление), "Отсутствующий элемент").Существует();
КонецПроцедуры

// Методы нужные для тестов
Функция ЗагрузчикЗаглушка()
	Возврат ЭтотОбъект;
КонецФункции

Функция ПолучитьКонтекстПоПути(КонтекстЯдра, Путь) Экспорт
	Возврат ЭтотОбъект;
КонецФункции

Процедура УспешныйМетод() Экспорт
КонецПроцедуры

Процедура ПадающийМетод() Экспорт
	ВызватьИсключение "БАБАХ!!!";
КонецПроцедуры

